import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'
apply from: 'build.properties'

minecraft {
    runDir = "eclipse/"
}

project.version = getVersion()
project.ext.shortVersion = getShortVersion()

minecraft {	
    replaceIn "Reference.java"
    replace "@VERSION@", project.shortVersion
    replace "@MODID@", project.archivesBaseName
    replace "@MODNAME@", project.longname
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

tasks["sourceJar"].classifier = 'sources'

tasks.build.dependsOn('deobfJar', 'sourceJar')
install.dependsOn build

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand project.properties
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

repositories {
    mavenLocal()
}

artifacts {
    archives jar
    archives deobfJar
    archives sourceJar
}

def getShortVersion() {
    def stdout = new ByteArrayOutputStream()
    exec {
        executable "git"
        args "rev-list", "--count", "HEAD"
        standardOutput = stdout
    }
    def revision = stdout.toString().trim()
    return "${major}.${minor}.${patch}.${revision}"
}

def getVersion() {
	return getShortVersion() + "-" + getArtifactID()
}

def getArtifactID() {
    def stdout = new ByteArrayOutputStream()
    exec {
        executable "git"
        args "rev-parse", "--abbrev-ref", "HEAD"
        standardOutput = stdout
    }
    def branch = stdout.toString().trim()
    if(branch == "HEAD") {
        stdout.reset()
        exec {
            executable "git"
            args "rev-parse", "--short", "HEAD"
            standardOutput = stdout
        }
        branch = stdout.toString().trim()
    }
	return "${branch == 'master' ? '' : branch.replace('/', '_') + '-'}${minecraft.version}"
}

